ext {
    junitVersion = project.hasProperty('junitVersion') ? rootProject.ext.junitVersion : '4.13.2'
    androidxAppCompatVersion = project.hasProperty('androidxAppCompatVersion') ? rootProject.ext.androidxAppCompatVersion : '1.7.1'
    androidxJunitVersion = project.hasProperty('androidxJunitVersion') ? rootProject.ext.androidxJunitVersion : '1.3.0'
    androidxEspressoCoreVersion = project.hasProperty('androidxEspressoCoreVersion') ? rootProject.ext.androidxEspressoCoreVersion : '3.7.0'

    // Read HLS configuration from gradle.properties (set by hook script)
    // Default to 'true' for backward compatibility
    includeHls = project.findProperty('nativeAudio.hls.include') ?: 'true'
}

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
    }
}

apply plugin: 'com.android.library'

android {
    namespace = "ee.forgr.audio.nativeaudio"
    compileSdk = project.hasProperty('compileSdkVersion') ? rootProject.ext.compileSdkVersion : 36
    defaultConfig {
        minSdkVersion project.hasProperty('minSdkVersion') ? rootProject.ext.minSdkVersion : 24
        targetSdkVersion project.hasProperty('targetSdkVersion') ? rootProject.ext.targetSdkVersion : 36
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError = false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    // Exclude StreamAudioAsset when HLS is disabled
    // StreamAudioAsset depends on HlsMediaSource which is only available with media3-exoplayer-hls
    sourceSets {
        main {
            if (includeHls != 'true') {
                java.exclude '**/StreamAudioAsset.java'
            }
        }
    }
}

repositories {
    google()
    mavenCentral()
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':capacitor-android')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation 'androidx.media3:media3-exoplayer:1.9.0'

    // HLS (m3u8) streaming support - optional dependency
    // When disabled, reduces APK size by ~4MB
    // Configure via capacitor.config.ts: plugins.NativeAudio.hls = false
    if (includeHls == 'true') {
        implementation 'androidx.media3:media3-exoplayer-hls:1.9.0'
    }

    implementation 'androidx.media3:media3-session:1.9.0'
    implementation 'androidx.media3:media3-transformer:1.9.0'
    implementation 'androidx.media3:media3-ui:1.8.0'
    implementation 'androidx.media3:media3-database:1.9.0'
    implementation 'androidx.media3:media3-common:1.9.0'
    // Media notification support
    implementation 'androidx.media:media:1.7.1'
    implementation 'androidx.core:core:1.13.1'
}
